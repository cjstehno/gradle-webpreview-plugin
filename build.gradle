plugins {
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id "com.stehno.gradle.webpreview" version "0.1.1" // Note: this should be the PUBLISHED version
}

version = "0.2.0"
group = "com.stehno.gradle"

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

configurations {
    groovyDoc
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'io.undertow:undertow-core:1.4.6.Final'

    // TODO: figure out how to pull the version # from the Gradle distro
    groovyDoc "org.codehaus.groovy:groovy-groovydoc:2.4.7"
    groovyDoc "org.codehaus.groovy:groovy-ant:2.4.7"

    testCompile 'junit:junit:4.12'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

groovydoc {
    groovyClasspath = configurations.groovyDoc
}

license {
    header rootProject.file('license_header.txt')
    ext.name = 'Christopher J. Stehno'
    ext.email = 'chris@stehno.com'
    ext.year = Calendar.instance.get(Calendar.YEAR)
}

pluginBundle {
    website = 'http://cjstehno.github.io/gradle-webpreview-plugin/'
    vcsUrl = 'https://github.com/cjstehno/gradle-webpreview-plugin'
    description = 'Gradle plugin to preview local web content.'
    tags = ['gradle', 'groovy']

    plugins {
        webpreviewPlugin {
            id = 'com.stehno.gradle.webpreview'
            displayName = 'Gradle Web Preview Plugin'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task site(group: 'Documentation', description: 'Builds the documentation web site.', dependsOn: ['build', 'jacocoTestReport']) {
    doLast {
        logger.info 'Building documentation web site...'

        // copy the templates
        copy {
            from 'src/site'
            into 'build/site'
            include '**/*.html'
            expand([project_version: project.version, year: Calendar.instance.get(Calendar.YEAR)])
        }

        // copy the assets
        copy {
            from 'src/site'
            into 'build/site'
            include '**/stylesheets/**'
            include '**/javascripts/**'
            include '**/images/**'
        }

        copy {
            from 'build/reports'
            include '**/**'
            into 'build/site'
        }
    }
}

task publishSite(type: GradleBuild, group: 'Publishing', description: 'Publishes the documentation web site.', dependsOn: ['site']) {
    buildFile = 'publish.gradle'
    tasks = ['publishGhPages']
}

task updateVersion(group: 'Documentation', description: 'Updates the version in documentation based on the project version') {
    doLast {
        String newVersion = project.version
        String oldVersion = project.property('from')

        logger.lifecycle "Updating documentation versions from ${oldVersion} to ${newVersion}..."

        ant.replace(file: 'README.md', token: oldVersion, value: newVersion)
    }
}

task checkVersion(group: 'Verification', description: 'Verify that the project version is reflected in the documentation.') {
    doLast {
        logger.lifecycle "Verifying that the documentation references version ${project.version}..."

        // Not the most efficient way to do this but should be ok for now
        boolean documented = ['README.md'].every { f ->
            boolean ok = project.file(f).text.contains(project.version)

            logger.info " - Checking: $f: $ok"
            ok
        }

        assert documented, 'The documented project version does not match the project version: Run "./gradlew updateVersion -Pfrom=OLD_VERSION" and try again.'
    }
}

webPreview {
    monitorPort = 20202
    resourceDir = file('build/site')
}