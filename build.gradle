plugins {
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id "com.stehno.gradle.webpreview" version "0.2.0" // Note: this should be the PUBLISHED version
    id 'com.stehno.gradle.site' version '0.0.3'
}

version = '0.4.0'
group = 'com.stehno.gradle'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    implementation 'io.undertow:undertow-core:1.4.6.Final'

    testImplementation 'junit:junit:4.12'
    testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

license {
    header rootProject.file('license_header.txt')
    ext.name = 'Christopher J. Stehno'
    ext.email = 'chris@stehno.com'
    ext.year = Calendar.instance.get(Calendar.YEAR)
}

pluginBundle {
    website = 'http://cjstehno.github.io/gradle-webpreview-plugin/'
    vcsUrl = 'https://github.com/cjstehno/gradle-webpreview-plugin'
    description = 'Gradle plugin to preview local web content.'
    tags = ['gradle', 'groovy']

    plugins {
        webpreviewPlugin {
            id = 'com.stehno.gradle.webpreview'
            displayName = 'Gradle Web Preview Plugin'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.site.dependsOn = ['build', 'jacocoTestReport']

task publishSite(type: GradleBuild, group: 'Publishing', description: 'Publishes the documentation web site.', dependsOn: ['site']) {
    buildFile = 'publish.gradle'
    tasks = ['publishGhPages']
}
