plugins {
    id 'com.github.hierynomus.license' version '0.12.1'
    id 'com.gradle.plugin-publish' version '0.9.4'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'maven-publish' // just for local testing
}

version = "0.0.1"
group = "com.stehno.gradle"

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

configurations {
    groovyDoc
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    // TODO: check on more recent versions of jetty
    compile 'org.eclipse.jetty.aggregate:jetty-all:7.6.15.v20140411'

    // TODO: figure out how to pull the version # from the Gradle distro
    groovyDoc "org.codehaus.groovy:groovy-groovydoc:2.4.7"
    groovyDoc "org.codehaus.groovy:groovy-ant:2.4.7"

    testCompile 'junit:junit:4.12'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

groovydoc {
    groovyClasspath = configurations.groovyDoc
}

license {
    header rootProject.file('license_header.txt')
    ext.name = 'Christopher J. Stehno'
    ext.email = 'chris@stehno.com'
    ext.year = Calendar.instance.get(Calendar.YEAR)
}

pluginBundle {
    website = 'http://cjstehno.github.io/gradle-webpreview-plugin/'
    vcsUrl = 'https://github.com/cjstehno/gradle-webpreview-plugin'
    description = 'Gradle plugin to preview local web content.'
    tags = ['gradle', 'groovy']

    plugins {
        webpreviewPlugin {
            id = 'com.stehno.gradle.webpreview'
            displayName = 'Gradle Web Preview Plugin'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.0"
}
